(* ========== Raw Syntax ========== *)

module Raw = struct
  type t =
    | Ident of string (* x *)
    | App of t * t (* a b *)
    | Lam of binder list * t (* fun x y : T => e *)
    | Pi of binder_group * t (* (x y : A) → B *)
    | Arrow of t * t (* A → B *)
    | Let of string * t option * t * t (* let x : A := a; b *)
    | U (* Type *)
    | Sigma of binder_group * t (* (x y : A) × B *)
    | Prod of t * t (* A × B *)
    | Pair of t * t (* (a, b) *)
    | Proj1 of t (* fst e *)
    | Proj2 of t (* fst e *)
    | Eq of t * t (* a = b, desugars to Eq _ a b *)
    | Int (* Int *)
    | IntLit of int (* 0 *)
    | Add of t * t (* a + b *)
    | Sub of t * t (* a - b *)
    | Ann of t * t (* generated by defs *)
    | Sorry (* sorry *)

  and binder = string option * t option
  and binder_group = string option list * t

  (* Constructor: name * params * return_type_opt *)
  type ctor = string * binder list * t option

  type item =
    | Def of string * t
    | Example of t
    | Inductive of string * binder_group list * t option * ctor list
    | Import of string (* import Foo.Bar *)

  type program = item list
end

(* ========== Core Syntax ========== *)

module Idx = struct
  type t = Idx of int

  let to_int (Idx l) = l
end

module Lvl = struct
  type t = Lvl of int

  let to_int (Lvl l) = l
end

type ty =
  | TyU
  | TyPi of string option * ty * ty
  | TySigma of string option * ty * ty
  | TyInt
  | TyEl of tm

and tm =
  | TmVar of Idx.t
  | TmConst of string (* global constant *)
  | TmLam of string option * ty * tm
  | TmApp of tm * tm
  | TmPiHat of string option * tm * tm
  | TmSigmaHat of string option * tm * tm
  | TmMkSigma of ty * ty * tm * tm
  | TmProj1 of tm
  | TmProj2 of tm
  | TmIntLit of int
  | TmIntHat
  | TmAdd of tm * tm
  | TmSub of tm * tm
  | TmSorry of int * ty
  | TmLet of string * ty * tm * tm

(* Weak head normal form *)
type vl_ty =
  | VTyU
  | VTyPi of string option * vl_ty * clos_ty
  | VTySigma of string option * vl_ty * clos_ty
  | VTyInt
  | VTyEl of neutral

(* Weak head normal form *)
and vl_tm =
  | VTmNeutral of neutral
  | VTmLam of string option * vl_ty * clos_tm
  | VTmPiHat of string option * vl_tm * clos_tm
  | VTmSigmaHat of string option * vl_tm * clos_tm
  | VTmMkSigma of string option * vl_ty * clos_ty * vl_tm * vl_tm
  | VTmIntLit of int
  | VTmIntHat
  | VTmAdd of vl_tm * vl_tm
  | VTmSub of vl_tm * vl_tm

and neutral = head * spine

and head =
  | HVar of Lvl.t (* de Bruijn level *)
  | HConst of string
  | HSorry of int * vl_ty

and spine = elim list

and elim =
  | EApp of vl_tm
  | EProj1
  | EProj2

and clos_ty = ClosTy of env * ty
and clos_tm = ClosTm of env * tm
and env = vl_tm list
