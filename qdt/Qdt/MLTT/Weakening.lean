import Qdt.MLTT.Judgements

namespace Qdt

theorem Ctx.get_weaken {m n}
    {Î“â‚ : Ctx 0 m} {Î“â‚‚ : Ctx m n}
    {x C}
    (i : Idx n) :
    Ctx.get (i.shiftAfter (n - m) 1) (Î“â‚.snoc âŸ¨x, CâŸ© ++ Î“â‚‚.shift 1) = (Ctx.get i (Î“â‚ ++ Î“â‚‚)).shiftAfter (n - m) 1 := by
  induction Î“â‚‚ with
  | nil =>
      simp only [Nat.sub_self, Idx.shiftAfter]
      rfl
  | @snoc n' Î“â‚‚ p ih =>
      have âŸ¨y, BâŸ© := p
      have hle : m â‰¤ n' := Î“â‚‚.le
      have âŸ¨i_val, hiâŸ© := i
      match i_val with
      | 0 =>
        rw [Nat.succ_sub Î“â‚‚.le]
        exact (Ty.shift_shift_comm (n' - m) B).symm
      | j + 1 =>
        have ih_spec := ih âŸ¨j, by omegaâŸ©
        simp only [Idx.shiftAfter] at ih_spec
        show Ctx.get (if _ then _ else _) _ = _
        simp [Ctx.get]
        rw [Nat.succ_sub hle, Ty.shift_shift_comm]
        split
        Â· have h2 : n' - m â‰¤ j := by omega
          simp only [h2] at ih_spec
          exact congrArg (Ty.shift 1) ih_spec
        Â· have h2 : Â¬ n' - m â‰¤ j := by omega
          simp only [h2] at ih_spec
          exact congrArg (Ty.shift 1) ih_spec

/-- Unified weakening theorem for all judgments. HoTT book A.2.2, wkgâ‚ and wkgâ‚‚. -/
theorem Derives.weaken {n m}
    {Î“ : Ctx 0 n} {Î“â‚ : Ctx 0 m} {Î“â‚‚ : Ctx m n}
    {ð’¿ : Judgement n} {x C}
    (hÎ“ : Î“ = Î“â‚ ++ Î“â‚‚)
    (hC : Î“â‚ âŠ¢ C type)
    (hð’¿ : Î“ âŠ¢ ð’¿) :
    (Î“â‚.snoc âŸ¨x, CâŸ© ++ Î“â‚‚.shift 1 âŠ¢ ð’¿.shiftAfter (n - m) 1) := by
  unfold Judgement.shiftAfter
  let Î“'' := Î“â‚.snoc âŸ¨x, CâŸ© ++ Î“â‚‚.shift 1
  induction hð’¿ generalizing Î“â‚ x C with
  -- Context well-formedness
  | empty => cases Î“â‚‚ with | nil => exact .extend hC
  | @extend _ _ _ _ hA ih =>
      cases Î“â‚‚ with
      | nil => cases hÎ“ with | refl => exact .extend hC
      | snoc =>
          obtain âŸ¨hÎ“', rflâŸ© := Tele.snoc.inj hÎ“
          exact .extend (ih hÎ“' hC)
  -- Type well-formedness
  | @pi_form _ Î“' y D _ _ _ ihA ihB =>
      apply Derives.pi_form (ihA hÎ“ hC)
      have hÎ“' : Î“'.snoc âŸ¨y, DâŸ© = Î“â‚ ++ Î“â‚‚.snoc âŸ¨y, DâŸ© := by rw [hÎ“]; rfl
      have ihB' := @ihB Î“â‚ (Î“â‚‚.snoc âŸ¨y, DâŸ©) x C hÎ“' hC
      simpa [Ctx.shift_snoc, Nat.succ_sub Î“â‚‚.le] using ihB'
  -- Type equality
  | trans_eq_ty _ _ ihAB ihBC => exact .trans_eq_ty (ihAB hÎ“ hC) (ihBC hÎ“ hC)
  | @pi_form_eq _ Î“' y D _ _ _ hA hB ihA ihB =>
      apply Derives.pi_form_eq (ihA hÎ“ hC)
      have hÎ“' : Î“'.snoc âŸ¨y, DâŸ© = Î“â‚ ++ Î“â‚‚.snoc âŸ¨y, DâŸ© := by rw [hÎ“]; rfl
      have ihB' := @ihB Î“â‚ (Î“â‚‚.snoc âŸ¨y, DâŸ©) x C hÎ“' hC
      simpa [Ctx.shift_snoc, Nat.succ_sub Î“â‚‚.le] using ihB'
  | el_form_eq _ ih => exact .el_form_eq (ih hÎ“ hC)
  -- Term equality
  | trans_eq_tm _ _ ihab ihbc => exact .trans_eq_tm (ihab hÎ“ hC) (ihbc hÎ“ hC)
  | @pi_intro_eq _ Î“' y _ _ D _ _ hA hB ihA ihB =>
      apply Derives.pi_intro_eq (ihA hÎ“ hC)
      have hÎ“' : Î“'.snoc âŸ¨y, DâŸ© = Î“â‚ ++ Î“â‚‚.snoc âŸ¨y, DâŸ© := by rw [hÎ“]; rfl
      have ihB' := @ihB Î“â‚ (Î“â‚‚.snoc âŸ¨y, DâŸ©) x C hÎ“' hC
      simpa [Ctx.shift_snoc, Nat.succ_sub Î“â‚‚.le] using ihB'
  | @pi_elim_eq n' Î“' y _ _ a' _ D E _ _ ihf iha =>
      let k := n' - m
      have ihf' : Î“'' âŠ¢ _ â‰¡ _ : .pi âŸ¨y, D.shiftAfter k 1âŸ© (E.shiftAfter (k + 1) 1) := ihf hÎ“ hC
      have h := Derives.pi_elim_eq ihf' (iha hÎ“ hC)
      rw [Ty.shift_subst_comm] at h
      exact h
  | @pi_comp n' Î“' y a' b' D E _ _ ihB iha =>
      let k := n' - m
      have hÎ“' : Î“'.snoc âŸ¨y, DâŸ© = Î“â‚ ++ Î“â‚‚.snoc âŸ¨y, DâŸ© := by rw [hÎ“]; rfl
      have ihB' : Î“''.snoc âŸ¨y, D.shiftAfter k 1âŸ© âŠ¢ b'.shiftAfter (k + 1) 1 : E.shiftAfter (k + 1) 1 := by
        simpa [Ctx.shift_snoc, Nat.succ_sub Î“â‚‚.le] using @ihB Î“â‚ (Î“â‚‚.snoc âŸ¨y, DâŸ©) x C hÎ“' hC
      have h := Derives.pi_comp ihB' (iha hÎ“ hC)
      rw [Ty.shift_subst_comm, Tm.shift_subst_comm] at h
      exact h
  | @pi_uniq n' Î“' y D E f' hf ih =>
      have ihf := @ih Î“â‚ Î“â‚‚ x C hÎ“ hC
      have h := Derives.pi_uniq ihf
      have idx_shift :
          Idx.shiftAfter (n' - m + 1) 1 âŸ¨0, Nat.zero_lt_succ n'âŸ© = âŸ¨0, Nat.zero_lt_succ (n' + 1)âŸ© := by
        simp [Idx.shiftAfter]
      simp [Tm.shiftAfter, Tm.shift_shift_comm]
      exact h
  | conv_eq_tm _ _ ihheq ihhAB => exact .conv_eq_tm (ihheq hÎ“ hC) (ihhAB hÎ“ hC)
  -- Typing
  | @var n' Î“' _ i ih =>
      simp only [Tm.shiftAfter]
      subst hÎ“
      have hget := @Ctx.get_weaken m n' Î“â‚ Î“â‚‚ x C i
      rw [â† hget]
      exact .var (ih rfl hC) (i.shiftAfter (n' - m) 1)
  | @pi_intro _ Î“' y D body B hA hB ihA ihB =>
      apply Derives.pi_intro (ihA hÎ“ hC)
      have hÎ“' : Î“'.snoc âŸ¨y, DâŸ© = Î“â‚ ++ Î“â‚‚.snoc âŸ¨y, DâŸ© := by rw [hÎ“]; rfl
      have ihB' := @ihB Î“â‚ (Î“â‚‚.snoc âŸ¨y, DâŸ©) x C hÎ“' hC
      simpa [Ctx.shift_snoc, Nat.succ_sub Î“â‚‚.le] using ihB'
  | @pi_elim n' Î“' f a' y D E _ _ ihf iha =>
      let k := n' - m
      have ihf' : Î“'' âŠ¢ _ : .pi âŸ¨y, D.shiftAfter k 1âŸ© (E.shiftAfter (k + 1) 1) := ihf hÎ“ hC
      have iha' : Î“'' âŠ¢ _ : D.shiftAfter k 1 := iha hÎ“ hC
      have h := Derives.pi_elim ihf' iha'
      have eq : (E.shiftAfter (k + 1) 1)[a'.shiftAfter k 1] = E[a'].shiftAfter k 1 := E.shift_subst_comm k a'
      rw [eq] at h
      exact h
  | _ => constructor <;> apply_rules

theorem Derives.presup {n}
    {Î“ : Ctx 0 n}
    {ð’¿ : Judgement n}
    (hð’¿ : Î“ âŠ¢ ð’¿) :
    (Î“ âŠ¢) := by
  induction hð’¿ with
  | empty => constructor
  | extend => constructor; assumption
  | _ => assumption

theorem Ctx.WF.drop {n}
    {Î“ : Ctx 0 n} {x A} :
    (Î“.snoc âŸ¨x, AâŸ© âŠ¢) â†’
    (Î“ âŠ¢)
  | .extend hA => hA.presup

theorem Ctx.subst_snoc {m n x B} (a : Tm m) (Î“ : Ctx (m + 1) (n + 1)) :
    Ctx.subst a (Î“.snoc âŸ¨x, BâŸ©) =
    (Ctx.subst a Î“).snoc âŸ¨x, B.substAt âŸ¨n - m, by omegaâŸ© (Nat.add_sub_cancel' (Nat.le_of_succ_le_succ Î“.le) â–¸ a.shift (n - m))âŸ© :=
  rfl

private theorem addrc {n k : Nat} : n + 1 + k = n + k + 1 := Nat.add_right_comm n 1 k

theorem Tm.cast_shiftAfter {nâ‚ nâ‚‚ : Nat} (h : nâ‚ = nâ‚‚) (m s : Nat) (t : Tm nâ‚) :
    (h â–¸ t).shiftAfter m s = (congrArg (Â· + s) h â–¸ t.shiftAfter m s) := by cases h; rfl
theorem Ty.cast_shiftAfter {nâ‚ nâ‚‚ : Nat} (h : nâ‚ = nâ‚‚) (m s : Nat) (T : Ty nâ‚) :
    (h â–¸ T).shiftAfter m s = (congrArg (Â· + s) h â–¸ T.shiftAfter m s) := by cases h; rfl

mutual

theorem Ty.shiftAfter_succ {n : Nat} (m k : Nat) :
    (T : Ty n) â†’ T.shiftAfter m (k + 1) = (T.shiftAfter m k).shiftAfter m 1
  | ð‘¢ => rfl
  | .el t => congrArg Ty.el (Tm.shiftAfter_succ m k t)
  | .pi âŸ¨x, AâŸ© B => by
      simp only [Ty.shiftAfter]
      have ihA := Ty.shiftAfter_succ m k A
      have ihB := Ty.shiftAfter_succ (m + 1) k B
      simp only [ihA, Ty.cast_shiftAfter addrc (m + 1) 1, ihB]
termination_by structural T => T

theorem Tm.shiftAfter_succ {n : Nat} (m k : Nat) :
    (t : Tm n) â†’ t.shiftAfter m (k + 1) = (t.shiftAfter m k).shiftAfter m 1
  | .var âŸ¨i, hiâŸ© => by
      simp only [Tm.shiftAfter, Idx.shiftAfter]
      by_cases h : m â‰¤ i
      Â· have h' : m â‰¤ i + k := Nat.le_add_right_of_le h
        simp only [h, h', â†“reduceIte, Nat.add_assoc]
      Â· simp only [h, â†“reduceIte]
  | .const _ => rfl
  | .lam âŸ¨x, AâŸ© body => by
      simp only [Tm.shiftAfter]
      have ihA := Ty.shiftAfter_succ m k A
      have ihBody := Tm.shiftAfter_succ (m + 1) k body
      simp only [ihA, Tm.cast_shiftAfter addrc (m + 1) 1, ihBody]
  | .app f a => by
      simp only [Tm.shiftAfter, Tm.shiftAfter_succ m k f, Tm.shiftAfter_succ m k a]
  | .piHat x a b => by
      simp only [Tm.shiftAfter]
      have iha := Tm.shiftAfter_succ m k a
      have ihb := Tm.shiftAfter_succ (m + 1) k b
      simp only [iha, Tm.cast_shiftAfter addrc (m + 1) 1, ihb]
  | .proj i t => congrArg (Tm.proj i) (Tm.shiftAfter_succ m k t)
  | .letE x ty t body => by
      simp only [Tm.shiftAfter]
      have ihty := Ty.shiftAfter_succ m k ty
      have iht := Tm.shiftAfter_succ m k t
      have ihbody := Tm.shiftAfter_succ (m + 1) k body
      simp only [ihty, iht, Tm.cast_shiftAfter addrc (m + 1) 1, ihbody]
termination_by structural t => t

end

theorem Tm.shift_succ {n : Nat} (k : Nat) (t : Tm n) : t.shift (k + 1) = (t.shift k).shift 1 :=
  Tm.shiftAfter_succ 0 k t

end Qdt
