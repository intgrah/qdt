import Qdt.MLTT.Context
import Qdt.MLTT.Substitution.Basic
import Lean.Elab.Tactic

namespace Qdt

open Lean (Name)

section Definitions

inductive Judgement (n : Nat) : Type
  | Ctx.WF : Judgement n
  | Ty.WF : Ty n â†’ Judgement n
  | Tm.HasType : Tm n â†’ Ty n â†’ Judgement n
  | Tm.Eq : Tm n â†’ Tm n â†’ Ty n â†’ Judgement n
  | Ty.Eq : Ty n â†’ Ty n â†’ Judgement n

set_option hygiene false

notation:50 Î“ " âŠ¢ " ð’¿ => Derives Î“ ð’¿
notation:50 Î“ " âŠ¢ " => Derives Î“ (Judgement.Ctx.WF)
notation:50 Î“ " âŠ¢ " A "type" => Derives Î“ (Judgement.Ty.WF A)
notation:50 Î“ " âŠ¢ " a " : " A => Derives Î“ (Judgement.Tm.HasType a A)
notation:50 Î“ " âŠ¢ " A "â‰¡ " B " : " C => Derives Î“ (Judgement.Tm.Eq A B C)
notation:50 Î“ " âŠ¢ " A "â‰¡" B "type" => Derives Î“ (Judgement.Ty.Eq A B)

inductive Derives : âˆ€ {n}, Ctx 0 n â†’ Judgement n â†’ Prop
  -- ## Context well-formedness
  | empty :
      (Tele.nil âŠ¢)
  | extend {Î“ x A} :
      (Î“ âŠ¢ A type) â†’
      (Î“.snoc âŸ¨x, AâŸ© âŠ¢)
  -- ## Type well-formedness
  | u_form {Î“} :
      (Î“ âŠ¢) â†’
      (Î“ âŠ¢ ð‘¢ type)
  | el_form {Î“ t} :
      (Î“ âŠ¢ t : ð‘¢) â†’
      (Î“ âŠ¢ .el t type)
  | pi_form {Î“ x A B} :
      (Î“ âŠ¢ A type) â†’
      (Î“.snoc âŸ¨x, AâŸ© âŠ¢ B type) â†’
      (Î“ âŠ¢ .pi âŸ¨x, AâŸ© B type)
  -- ## Definitional equality of types
  | refl_eq_ty {Î“ A} :
      (Î“ âŠ¢ A type) â†’
      (Î“ âŠ¢ A â‰¡ A type)
  | symm_eq_ty {Î“ A B} :
      (Î“ âŠ¢ A â‰¡ B type) â†’
      (Î“ âŠ¢ B â‰¡ A type)
  | trans_eq_ty {Î“ A B C} :
      (Î“ âŠ¢ A â‰¡ B type) â†’
      (Î“ âŠ¢ B â‰¡ C type) â†’
      (Î“ âŠ¢ A â‰¡ C type)
  -- ## Definitional equality of terms
  | el_form_eq {Î“ tâ‚ tâ‚‚} :
      (Î“ âŠ¢ tâ‚ â‰¡ tâ‚‚ : .u) â†’
      (Î“ âŠ¢ .el tâ‚ â‰¡ .el tâ‚‚ type)
  | pi_form_eq {Î“ x Aâ‚ Aâ‚‚ Bâ‚ Bâ‚‚} :
      (Î“ âŠ¢ Aâ‚ â‰¡ Aâ‚‚ type) â†’
      (Î“.snoc âŸ¨x, Aâ‚âŸ© âŠ¢ Bâ‚ â‰¡ Bâ‚‚ type) â†’
      (Î“ âŠ¢ .pi âŸ¨x, Aâ‚âŸ© Bâ‚ â‰¡ .pi âŸ¨x, Aâ‚‚âŸ© Bâ‚‚ type)
  | refl_eq_tm {Î“ a A} :
      (Î“ âŠ¢ a : A) â†’
      (Î“ âŠ¢ a â‰¡ a : A)
  | symm_eq_tm {Î“ a b A} :
      (Î“ âŠ¢ a â‰¡ b : A) â†’
      (Î“ âŠ¢ b â‰¡ a : A)
  | trans_eq_tm {Î“ a b c A} :
      (Î“ âŠ¢ a â‰¡ b : A) â†’
      (Î“ âŠ¢ b â‰¡ c : A) â†’
      (Î“ âŠ¢ a â‰¡ c : A)
  | pi_intro_eq {Î“ x bâ‚ bâ‚‚ Aâ‚ Aâ‚‚ B} :
      (Î“ âŠ¢ Aâ‚ â‰¡ Aâ‚‚ type) â†’
      (Î“.snoc âŸ¨x, Aâ‚âŸ© âŠ¢ bâ‚ â‰¡ bâ‚‚ : B) â†’
      (Î“ âŠ¢ .lam âŸ¨x, Aâ‚âŸ© bâ‚ â‰¡ .lam âŸ¨x, Aâ‚‚âŸ© bâ‚‚ : .pi âŸ¨x, Aâ‚âŸ© B)
  | pi_elim_eq {Î“ x fâ‚ fâ‚‚ aâ‚ aâ‚‚ A B} :
      (Î“ âŠ¢ fâ‚ â‰¡ fâ‚‚ : .pi âŸ¨x, AâŸ© B) â†’
      (Î“ âŠ¢ aâ‚ â‰¡ aâ‚‚ : A) â†’
      (Î“ âŠ¢ fâ‚.app aâ‚ â‰¡ fâ‚‚.app aâ‚‚ : B[aâ‚])
  | pi_comp {Î“ x a b A B} :
      (Î“.snoc âŸ¨x, AâŸ© âŠ¢ b : B) â†’
      (Î“ âŠ¢ a : A) â†’
      (Î“ âŠ¢ (Tm.lam âŸ¨x, AâŸ© b).app a â‰¡ b[a] : B[a])
  | pi_uniq {Î“ x A B f} :
      (Î“ âŠ¢ f : .pi âŸ¨x, AâŸ© B) â†’
      (Î“ âŠ¢ f â‰¡ .lam âŸ¨x, AâŸ© ((â†‘f).app (.var 0)) : .pi âŸ¨x, AâŸ© B)
  | conv_eq_tm {Î“ A B a b} :
      (Î“ âŠ¢ a â‰¡ b : A) â†’
      (Î“ âŠ¢ A â‰¡ B type) â†’
      (Î“ âŠ¢ a â‰¡ b : B)
  -- ## Typing
  | var {n Î“} :
      (Î“ âŠ¢) â†’
      (i : Idx n) â†’
      (Î“ âŠ¢ .var i : Î“.get i)
  | const {Î“ x} :
      (Î“ âŠ¢) â†’
      (Î“ âŠ¢ .const x : ð‘¢) -- TODO: add support for global constants
  | pi_intro {Î“ x A body B} :
      (Î“ âŠ¢ A type) â†’
      (Î“.snoc âŸ¨x, AâŸ© âŠ¢ body : B) â†’
      (Î“ âŠ¢ .lam âŸ¨x, AâŸ© body : .pi âŸ¨x, AâŸ© B)
  | pi_elim {Î“ f a x A B} :
      (Î“ âŠ¢ f : .pi âŸ¨x, AâŸ© B) â†’
      (Î“ âŠ¢ a : A) â†’
      (Î“ âŠ¢ f.app a : B[a])
  | conv_has_type {Î“ e A B} :
      (Î“ âŠ¢ e : A) â†’
      (Î“ âŠ¢ A â‰¡ B type) â†’
      (Î“ âŠ¢ e : B)

end Definitions

@[app_unexpander Derives]
def Derives.unexpand : Lean.PrettyPrinter.Unexpander
  | `($_ $Î“ Judgement.Ctx.WF) => `($Î“ âŠ¢)
  | `($_ $Î“ (Judgement.Ty.WF $A)) => `($Î“ âŠ¢ $A type)
  | `($_ $Î“ (Judgement.Tm.HasType $a $A)) => `($Î“ âŠ¢ $a : $A)
  | `($_ $Î“ (Judgement.Tm.Eq $a $b $A)) => `($Î“ âŠ¢ $a â‰¡ $b : $A)
  | `($_ $Î“ (Judgement.Ty.Eq $A $B)) => `($Î“ âŠ¢ $A â‰¡ $B type)
  | _ => throw ()

open Lean Elab Tactic Meta in
/-- Try to guess the correct constructor and apply a closing tactic -/
elab "derives_constructor" closing:tacticSeq : tactic => do
  let ctors := #[
    `Derives.empty,
    `Derives.extend,
    `Derives.u_form,
    `Derives.el_form,
    `Derives.pi_form,
    `Derives.refl_eq_ty,
    `Derives.symm_eq_ty,
    `Derives.trans_eq_ty,
    `Derives.el_form_eq,
    `Derives.pi_form_eq,
    `Derives.refl_eq_tm,
    `Derives.symm_eq_tm,
    `Derives.trans_eq_tm,
    `Derives.pi_intro_eq,
    `Derives.pi_elim_eq,
    `Derives.pi_comp,
    `Derives.pi_uniq,
    `Derives.conv_eq_tm,
    `Derives.var,
    `Derives.const,
    `Derives.pi_intro,
    `Derives.pi_elim,
    `Derives.conv_has_type,
  ]
  for ctor in ctors do
    let s â† saveState
    try
      evalTactic (â† `(tactic| apply $(mkIdent ctor)))
      evalTactic (â† `(tactic| all_goals $closing))
      return
    catch _ =>
      restoreState s
      continue
  throwError "derives_constructor: no Derives constructor applies"

end Qdt
