import Qdt.Tele

namespace Qdt

open Lean (Name)

/-- de Bruijn indices -/
def Idx n := Fin n
deriving Repr, Hashable, DecidableEq, BEq

/-- Allow natural number literals to be used as de Bruijn indices -/
instance {n} [NeZero n] {i} : OfNat (Idx n) i where
  ofNat := Fin.ofNat n i

mutual

/-- Types -/
inductive Ty : Nat â†’ Type
  | u {n} : Ty n
  | pi {n} : Param n â†’ Ty (n + 1) â†’ Ty n
  /-- If t : ð‘¢, then El(t) type -/
  | el {n} : Tm n â†’ Ty n
deriving Repr, Hashable, DecidableEq, BEq

inductive Tm : Nat â†’ Type
  | var {n} : Idx n â†’ Tm n
  | const {n} : Name â†’ Tm n
  | lam {n} : Param n â†’ Tm (n + 1) â†’ Tm n
  | app {n} : Tm n â†’ Tm n â†’ Tm n
  | pi' {n} : Name â†’ Tm n â†’ Tm (n + 1) â†’ Tm n
  | proj {n} : Nat â†’ Tm n â†’ Tm n
  | letE {n} : Name â†’ Ty n â†’ Tm n â†’ Tm (n + 1) â†’ Tm n
deriving Repr, Hashable, DecidableEq, BEq

@[pp_using_anonymous_constructor]
inductive Param : Nat â†’ Type
  | mk {n} (name : Name) (ty : Ty n) : Param n
deriving Repr, Hashable, DecidableEq, BEq

end

notation "ð‘¢" => Ty.u

abbrev Ty.arrow {n} (ty : Ty n) := Ty.pi âŸ¨.anonymous, tyâŸ©

@[match_pattern]
def Tm.apps {n} : Tm n â†’ List (Tm n) â†’ Tm n :=
  List.foldl Tm.app

/- Point free! Point free! -/
def Ty.pis {a b} : Tele Param a b â†’ Ty b â†’ Ty a
  | .nil => id
  | .snoc bs param => pis bs âˆ˜ pi param

def Tm.lams {a b} : Tele Param a b â†’ Tm b â†’ Tm a
  | .nil => id
  | .snoc bs param => lams bs âˆ˜ lam param

end Qdt
