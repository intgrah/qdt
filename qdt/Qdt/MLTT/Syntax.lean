import Qdt.Tele
import Qdt.MLTT.Universe

namespace Qdt

open Lean (Name)

/-- de Bruijn indices -/
def Idx n := Fin n
deriving Repr, Hashable, DecidableEq, BEq

/-- Allow natural number literals to be used as de Bruijn indices -/
instance {n} [NeZero n] {i} : OfNat (Idx n) i where
  ofNat := Fin.ofNat n i

mutual

/-- Types -/
inductive Ty : Nat â†’ Type
  | u {n} : Universe â†’ Ty n
  | pi {n} : Param n â†’ Ty (n + 1) â†’ Ty n
  /-- If Î“ âŠ¢ t : ð‘¢ i, then Î“ âŠ¢ El(t) type -/
  | el {n} : Tm n â†’ Ty n
deriving Repr, Hashable, DecidableEq, BEq

inductive Tm : Nat â†’ Type
  | u' {n} : Universe â†’ Tm n
  | var {n} : Idx n â†’ Tm n
  | const {n} : Name â†’ List Universe â†’ Tm n
  | lam {n} : Param n â†’ Tm (n + 1) â†’ Tm n
  | app {n} : Tm n â†’ Tm n â†’ Tm n
  | pi' {n} : Name â†’ Tm n â†’ Tm (n + 1) â†’ Tm n
  | proj {n} : Nat â†’ Tm n â†’ Tm n
  | letE {n} : Name â†’ Ty n â†’ Tm n â†’ Tm (n + 1) â†’ Tm n
deriving Repr, Hashable, DecidableEq, BEq

@[pp_using_anonymous_constructor]
inductive Param : Nat â†’ Type
  | mk {n} (name : Name) (ty : Ty n) : Param n
deriving Repr, Hashable, DecidableEq, BEq

end

notation "ð‘¢" => Ty.u

abbrev Ty.arrow {n} (ty : Ty n) := Ty.pi âŸ¨.anonymous, tyâŸ©

@[match_pattern]
def Tm.apps {n} : Tm n â†’ List (Tm n) â†’ Tm n :=
  List.foldl Tm.app

/- Point free! Point free! -/
def Ty.pis {a b} : Tele Param a b â†’ Ty b â†’ Ty a
  | .nil => id
  | .snoc bs param => pis bs âˆ˜ pi param

def Ty.getResultUniverse? {n} : Ty n â†’ Option Universe
  | .u univ => some univ
  | .pi _ cod => cod.getResultUniverse?
  | .el _ => none

def Tm.lams {a b} : Tele Param a b â†’ Tm b â†’ Tm a
  | .nil => id
  | .snoc bs param => lams bs âˆ˜ lam param

private def lookup (subst : List (Name Ã— Universe)) (n : Name) : Universe :=
  match subst.find? (Â·.fst == n) with
  | some (_, u) => u
  | none => .level n

mutual

def Universe.subst (subst : List (Name Ã— Universe)) : Universe â†’ Universe
  | .level n => lookup subst n
  | .zero => .zero
  | .succ u => .succ (u.subst subst)
  | .max u v => .max (u.subst subst) (v.subst subst)

def Ty.substLevels {n} (subst : List (Name Ã— Universe)) : Ty n â†’ Ty n
  | .u u => .u (u.subst subst)
  | .pi âŸ¨name, tyâŸ© b => .pi âŸ¨name, ty.substLevels substâŸ© (b.substLevels subst)
  | .el t => .el (t.substLevels subst)

def Tm.substLevels {n} (subst : List (Name Ã— Universe)) : Tm n â†’ Tm n
  | .u' u => .u' (u.subst subst)
  | .var i => .var i
  | .const c us => .const c (us.map (Â·.subst subst))
  | .lam âŸ¨name, tyâŸ© b => .lam âŸ¨name, ty.substLevels substâŸ© (b.substLevels subst)
  | .app f a => .app (f.substLevels subst) (a.substLevels subst)
  | .pi' name a b => .pi' name (a.substLevels subst) (b.substLevels subst)
  | .proj i t => .proj i (t.substLevels subst)
  | .letE name ty rhs body =>
      .letE name (ty.substLevels subst) (rhs.substLevels subst) (body.substLevels subst)

def Param.substLevels {n} (subst : List (Name Ã— Universe)) : Param n â†’ Param n
  | âŸ¨name, tyâŸ© => âŸ¨name, ty.substLevels substâŸ©

end

mutual

def Ty.levelNames {n} : Ty n â†’ List Name
  | .u u => u.levelNames
  | .pi âŸ¨_, tyâŸ© b => ty.levelNames ++ b.levelNames
  | .el t => t.levelNames

def Tm.levelNames {n} : Tm n â†’ List Name
  | .u' u => u.levelNames
  | .var _ => []
  | .const _ us => us.flatMap Universe.levelNames
  | .lam âŸ¨_, tyâŸ© b => ty.levelNames ++ b.levelNames
  | .app f a => f.levelNames ++ a.levelNames
  | .pi' _ a b => a.levelNames ++ b.levelNames
  | .proj _ t => t.levelNames
  | .letE _ ty rhs body => ty.levelNames ++ rhs.levelNames ++ body.levelNames

def Param.levelNames {n} : Param n â†’ List Name
  | âŸ¨_, tyâŸ© => ty.levelNames

end

end Qdt
