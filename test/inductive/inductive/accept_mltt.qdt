inductive Empty where

example :
    (motive : Empty → Type) →
    (p : Empty) →
    motive p :=
  Empty.rec

inductive Unit where
  | unit

example :
    (motive : Unit → Type) →
    (unit : motive Unit.unit) →
    (p : Unit) →
    motive p :=
  Unit.rec

inductive Bool where
  | true
  | false

example :
    (motive : Bool → Type) →
    (true : motive Bool.true) →
    (false : motive Bool.false) →
    (p : Bool) →
    motive p :=
  Bool.rec

inductive Sigma (A : Type) (B : A → Type) : Type where
  | mk (fst : A) (snd : B fst) : Sigma A B

example :
    (A : Type) →
    (B : A → Type) →
    (motive : Sigma A B → Type) →
    (mk : (fst : A) → (snd : B fst) → motive (Sigma.mk A B fst snd)) →
    (p : Sigma A B) →
    motive p :=
  Sigma.rec

inductive Eq (A : Type) (a : A) : A → Type where
  | refl : Eq A a a

example :
    (A : Type) →
    (x : A) →
    (motive : (y : A) → Eq A x y → Type) →
    motive x (Eq.refl A x) →
    (y : A) →
    (t : Eq A x y) →
    motive y t :=
  Eq.rec

-- https://ncatlab.org/nlab/show/W-type
inductive W (C : Type) (A : C → Type) : Type where
  | tree (root : C) (subtr : A root → W C A) : W C A

example :
    (C : Type) →
    (A : C → Type) →
    (D : W C A → Type) →
    ((root : C) → (subtr : A root → W C A) → (subtr_D : (a : A root) → D (subtr a)) →
      D (W.tree C A root subtr)) →
    (w : W C A) →
    D w :=
  W.rec

-- TODO: universes
-- def Nat : Type := W Bool (Bool.rec (fun _ => Type) Unit Empty)
